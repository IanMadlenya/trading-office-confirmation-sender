ext {
    libs = libsMap(
            jackson: 'com.fasterxml.jackson.core:jackson-core:2.7.1',
            jackson_databind: 'com.fasterxml.jackson.core:jackson-databind:2.7.1-1',
            jackson_annotations: 'com.fasterxml.jackson.core:jackson-annotations:2.7.1',
            guava: 'com.google.guava:guava:19.0',
            spring: [
                boot_actuator: 'org.springframework.boot:spring-boot-starter-actuator',
                boot_web: 'org.springframework.boot:spring-boot-starter-web',
                boot_amqp: 'org.springframework.boot:spring-boot-starter-amqp',
                eureka_client: 'org.springframework.cloud:spring-cloud-starter-eureka',
                feign: 'org.springframework.cloud:spring-cloud-starter-feign',
                cloud_config: 'org.springframework.cloud:spring-cloud-starter-config'
            ],
            test: [
                    junit: 'junit:junit:4.12',
                    assertj: 'org.assertj:assertj-core:3.3.0',
                    mockito: 'org.mockito:mockito-core:2.0.42-beta'
            ],
            jasperreports: dependencies.create('net.sf.jasperreports:jasperreports:6.2.0') {
                exclude group: 'org.olap4j', module: 'olap4j'
            },
            itext: 'com.lowagie:itext:2.1.7',
            swift: dependencies.create('com.prowidesoftware:pw-swift-core:SRU2015-7.7') {
                exclude group: 'javax.xml', module: 'jaxb-api'
                exclude group: 'javax.xml', module: 'jaxb-impl'
            },
            jaxb: 'javax.xml.bind:jaxb-api:2.2.12'
    )
}

def libsMap(Map<String, ?> map) {
    def processMap
    processMap = { innerMap ->
        innerMap.each {
            if (it.value instanceof Map) {
                innerMap[it.key] = processMap(it.value)
            }
        }
        innerMap.asImmutable().withDefault {
            throw new InvalidUserDataException(
                    "No entry '$it' in dependency map (candidates: ${innerMap.keySet().sort().join(", ")})" )
        }
    }
    processMap(map)
}
